import java.text.SimpleDateFormat

// 1 hour
def TIMEOUT = 3600

// Set defaults for non-PR jobs
def pullSha = (env.PULL_PULL_SHA) ?: "main"
def pullBase = (env.PULL_BASE_REF) ?: "main"
def repoUrl = "https://github.com/metal3-io/baremetal-operator.git"
// Fetch the base branch and the pullSha, nothing else
def refspec = '+refs/heads/' + pullBase + ':refs/remotes/origin/' + pullBase + ' ' + pullSha

pipeline {
  environment {
    GINKGO_FOCUS="${GINKGO_FOCUS}"
  }
  agent none
  stages {
    stage("Run Baremetal Operator e2e tests") {
      matrix {
        agent { label "metal3-workers" }
        // Skip redfish on PRs, test all for periodic jobs
        when {
          anyOf {
            expression { env.SUSHY_TOOLS_PROTOCOL != "redfish" }
            triggeredBy 'TimerTrigger'
          }
        }
        axes {
          axis {
            name 'BMO_E2E_EMULATOR'
            values 'vbmc', 'sushy-tools'
          }
          axis {
            name 'SUSHY_TOOLS_PROTOCOL'
            values 'redfish', 'redfish-virtualmedia'
          }
        }
        excludes {
          // What we want to test here is VBMC with IPMI, sushy-tools with redfish and redfish-virtualmedia.
          // It is not possible to completely skip one of the axes though so we just exclude one VBMC combination.
          // Otherwise we would end up running two VBMC tests. Both of them would use IPMI since the SUSHY_TOOLS
          // parameter doesn't have anything to do with it.
          // TODO(lentzi90): Change the logic in BMO so we can use BMC_PROTOCOL instead of BMO_E2E_EMULATOR.
          exclude {
            axis {
              name 'BMO_E2E_EMULATOR'
              values 'vbmc'
            }
            axis {
              name 'SUSHY_TOOLS_PROTOCOL'
              values 'redfish'
            }
          }
        }
        environment {
          BMO_E2E_EMULATOR = "${BMO_E2E_EMULATOR}"
          SUSHY_TOOLS_PROTOCOL = "${SUSHY_TOOLS_PROTOCOL}"
        }
        stages {
          stage("Checkout source code") {
            steps {
              checkout scmGit(
                  branches: [[name: pullSha]],
                  userRemoteConfigs: [[url: repoUrl, credentialsId: "metal3-jenkins-github-token", refspec: refspec]],
                  extensions: [[$class: "WipeWorkspace"],
                  [$class: "CleanCheckout"],
                  [$class: "CleanBeforeCheckout"],
                  [$class: "PreBuildMerge", options: [mergeTarget: pullBase, mergeRemote: "origin"]],
                  [$class: "UserIdentity", name: "Test", email: "test@test.test"],
                  cloneOption(honorRefspec: true)],
                  submoduleCfg: [],)
              script {
                CURRENT_START_TIME = System.currentTimeMillis()
              }
            }
          }
          stage("Run Baremetal Operator e2e tests") {
            options {
              timeout(time: TIMEOUT, unit: "SECONDS")
              ansiColor("xterm")
            }
            steps {
              withCredentials([string(credentialsId: "metal3-clusterctl-github-token", variable: "GITHUB_TOKEN")]) {
                timestamps {
                  sh "./hack/ci-e2e.sh"
                }
              }
            }
            post {
              always {
                script {
                  CURRENT_END_TIME = System.currentTimeMillis()
                  if ((((CURRENT_END_TIME - CURRENT_START_TIME)/1000) - TIMEOUT) > 0) {
                      echo "Failed due to timeout"
                      currentBuild.result = "FAILURE"
                  }
                }
                archiveArtifacts "artifacts*.tar.gz"
                timestamps {
                  /* Clean up */
                  sh "make clean-e2e"
                }
              }
            }
          }
        }
      }
    }
  }
}
